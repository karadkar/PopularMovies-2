apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'io.github.karadkar.popularmovies.testing.OpenForTesting'
}


android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "io.github.karadkar.popularmovies"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "io.github.karadkar.popularmovies.app.TestAppJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'TMDB_API_KEY', tmdbApiKey
    }

    packagingOptions {
        pickFirst 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Libs.kotlinStdlib

    implementation Libs.appCompat
    implementation Libs.constraintLayout

    testImplementation Libs.junit
    androidTestImplementation Libs.atsRunner
    androidTestImplementation Libs.atsRules
    androidTestImplementation(Libs.barista) {
        exclude group: 'com.android.support'
        exclude group: 'org.jetbrains.kotlin'
    }

    // architecture component
    implementation Libs.archExtensions
    testImplementation Libs.archCoreTesting

    // koin
    implementation Libs.koinAndroidViewModel
    testImplementation Libs.koinTest
    androidTestImplementation Libs.koinTest

    // mockito
    testImplementation Libs.mockitoCore
    androidTestImplementation Libs.mockitoAndroid

    // RxJava
    implementation Libs.rxJava
    implementation Libs.rxAndroid

    // retrofit
    implementation Libs.retrofit
    implementation Libs.retrofitRxAdapter
    implementation Libs.gson
    implementation Libs.gsonConverter
    implementation Libs.okhttpLogger
    testImplementation Libs.mockWebServer
    testImplementation Libs.robolectric

    // room
    implementation Libs.roomRuntime
    kapt Libs.roomCompiler
    testImplementation Libs.roomTesting
    implementation Libs.roomRxJava2
}
